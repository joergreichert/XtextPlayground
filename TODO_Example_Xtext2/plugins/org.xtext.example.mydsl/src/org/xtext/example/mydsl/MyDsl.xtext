grammar org.xtext.example.mydsl.MyDsl with org.eclipse.xtext.common.Terminals

generate myDsl "http://www.xtext.org/example/mydsl/MyDsl"

Model: {Model}
	greetings+=Greeting*
	entities+=Entity*	
	('properties' '{'
		reference+=ReferenceChain (',' reference+=ReferenceChain)*
	'}')?	
;

ReferenceChain returns ReferenceExpression:
	Reference ({ReferenceChain.source=current} => '.' (target=[Referencable|ID]))*
;

Reference returns ReferenceExpression:
	{Reference}
	element=[Referencable|ID]
;

Referencable : 
	Entity | Feature
;

Entity :
	'entity' name=ID '{'
		features+=Feature*
	'}'
;

Feature :
	Attribute | FeatureReference
;

Attribute :
	name=ID '=' value=STRING
;

FeatureReference :
	name=ID '->' type=[Referencable|ID]
;
	
Greeting:
	'Hall\u00F6chen' name=ID '!';
	
terminal ID : '^'?('\u00E4'|'\u00F6'|'\u00FC'|'a'..'z'|'A'..'Z'|'\u00C4'|'\u00D6'|'\u00DC'|'_') ('\u00E4'|'\u00F6'|'\u00FC'|'\u00DF'|'a'..'z'|'\u00C4'|'\u00D6'|'\u00DC'|'A'..'Z'|'_'|'0'..'9')*;	

//\u00C4 -> Ä 
//\u00E4 -> ä 
//\u00D6 -> Ö 
//\u00F6 -> ö 
//\u00DC -> Ü 
//\u00FC -> ü 
//\u00DF -> ß